// print the Fibonacci sequence (10 elems)
// {s:10, 0,1,1,2,3,5,8,13,21,34}
(@main
  (@main
    0
    %arg1 <- 10 // the number of elems, 2 at least
    cjump %arg1 < 2 :bad_input
    
    // array init:
    %arg1 <<= 1
    %arg1 ++
    %count <- 1
    rdi <- %arg1
    call allocate 2
    mem rax 8 <- 0
    mem rax 16 <- 1
    
    // iteration:
    %arg1 <- mem rax 0
    %count <- 3
    :loop
    cjump %arg1 < %count :end_loop
    rdx @ rax %count 8
    rcx <- mem rdx -16
    rcx += mem rdx -8
    mem rdx 0 <- rcx
    %count ++
    goto :loop
    
    // encode and print array:
    :end_loop
    mem rsp -8 <- :array_encode_ret
    rdi <- rax
    call @array_encode 1
    
    :array_encode_ret
    rdi <- rax
    call print 1
    
    :bad_input
    return
  )
  
  // encode elements in an array as numbers in-place,
  // return the array address:
  (@array_encode
    1
    rax <- rdi
    rdi <- mem rax 0
    %count <- 1
    
    :loop_1
    cjump rdi < %count :end_loop_1
    rdx @ rax %count 8
    rcx <- mem rdx 0
    rcx <<= 1
    rcx ++
    mem rdx 0 <- rcx
    %count ++
    goto :loop_1
    
    :end_loop_1
    return
  )
)
