define void @main () {
    // Create arrays of size 10 and size 20 
    :entry
    int64 %Arr1Size
    int64 %Arr1SizeEncoded
    int64 %Arr2Size
    int64 %Arr2SizeEncoded
    int64 %Arr1PrintVal
    int64 %Arr2PrintVal
    %Arr1Size <- 10
    %Arr1SizeEncoded <- %Arr1Size << 1
    %Arr1SizeEncoded <- %Arr1SizeEncoded + 1
    %Arr2Size <- 20
    %Arr2SizeEncoded <- %Arr2Size << 1
    %Arr2SizeEncoded <- %Arr2SizeEncoded + 1

    // both initialized with 11
    int64[] %Arr1
    int64[] %Arr2
    %Arr1 <- call @Alloc1DArr (%Arr1SizeEncoded, 11)
    %Arr2 <- call @Alloc1DArr (%Arr2SizeEncoded, 11)

    // Get sum of both arrays
    int64 %Arr1Sum
    int64 %Arr2Sum
    %Arr1Sum <- call @CalcArrSum(%Arr1)
    %Arr2Sum <- call @CalcArrSum(%Arr2)

    // Print the sum of the larger array
    int64 %Arr1Larger
    %Arr1Larger <- %Arr2Sum < %Arr1Sum
    br %Arr1Larger :PrintArr1Sum :PrintArr2Sum

    // Print Array 1 sum
    :PrintArr1Sum
    %Arr1PrintVal <- %Arr1Sum << 1
    %Arr1PrintVal <- %Arr1PrintVal + 1
    call print(%Arr1PrintVal)
    return
    
    // Print array 2 sum
    :PrintArr2Sum
    %Arr2PrintVal <- %Arr2Sum << 1
    %Arr2PrintVal <- %Arr2PrintVal + 1
    call print(%Arr2PrintVal)
    return 
}

define int64 @CalcArrSum(int64[] %TargetArr) {
    :CalcArrEntry
    // Initialize loop and sum vars
    int64 %ArrLen
    int64 %Count
    int64 %ArrSum
    int64 %ContLoop
    int64 %CurrElm

    %ArrLen <- length %TargetArr 0
    %ArrLen <- %ArrLen >> 1
    %Count <- 0
    %ArrSum <- 0
    br :Loop

    // Get offset and curr elm
    :Loop
    %CurrElm <- %TargetArr[%Count]

    // Increment count and add to sum
    %Count <- %Count + 1
    %ArrSum <- %ArrSum + %CurrElm

    // Decide if we should continue looping or return
    %ContLoop <- %Count < %ArrLen
    br %ContLoop :Loop :Stop
    
    :Stop
    return %ArrSum
}

define int64[] @Alloc1DArr(int64 %ArrSize, int64 %InitElmVal) {
    :Alloc1DEntry
    // create array of appropriate size
    int64[] %NewArr
    %NewArr <- new Array(%ArrSize)

    int64 %ArrLen
    int64 %Count
    int64 %ArrSum
    int64 %ContLoop

    %ArrLen <- length %NewArr 0
    %ArrLen <- %ArrLen >> 1
    %Count <- 0
    %ArrSum <- 0
    br :Loop

    // Get offset and curr elm
    :Loop
    %NewArr[%Count] <- %InitElmVal

    // Increment count
    %Count <- %Count + 1

    // Decide if we should continue looping or return
    %ContLoop <- %Count < %ArrLen
    br %ContLoop :Loop :Stop
    
    :Stop
    return %NewArr
}
