// Args:    %val     : int
//
// Return:  %encoded : int
define @encode(%val) {
  %almost_encoded <- %val << 1
  %encoded <- %almost_encoded + 1
  return %encoded
}

// Args:    %val     : int
//
// Return:  %decoded : int
define @decode(%val) {
  %decoded <- %val >> 1
  return %decoded
}

// Return:  %queue_ptr : ptr
define @new_stack_or_queue() {
  // This allocates an empty queue, where the "elements" are all 0's
  %size <- 0
  %encoded_size <- call @encode(%size)
  %default <- 0
  %encoded_default <- call @encode(%default)
  %queue_ptr <- call allocate(%encoded_size, %encoded_default)
  return %queue_ptr
}

// Args:    %old_queue_ptr : ptr
//          %val           : int
//
// Return:  %new_queue_ptr : ptr
define @enqueue(%old_queue_ptr, %val) {

  // First, allocate a new array that's one element bigger than the old one
  // This should load the %size
  %old_size <- load %old_queue_ptr
  %new_size <- %old_size + 1
  %encoded_new_size <- call @encode(%new_size)
  %default <- 0
  %encoded_default <- call @encode(%default)
  %new_queue_ptr <- call allocate(%encoded_new_size, %encoded_default)

  // Call a recursive helper function to fill the rest of the array
  %final_queue_ptr <- call @refill_queue(%new_queue_ptr, %old_queue_ptr, %old_size)

  // Finally, write the new value into the last idx of the new array
  %encoded_val <- call @encode(%val)
  %offset <- %new_size * 8
  %curr_idx <- %new_queue_ptr + %offset
  store %curr_idx <- %encoded_val

  return %final_queue_ptr
}

// Args:    %new_queue_ptr   : ptr
//          %old_queue_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_queue_ptr   : ptr

// Args:    %old_stack_ptr : ptr
//          %val           : int
//
// Return:  %new_stack_ptr : ptr
define @push(%old_stack_ptr, %val) {

  // First, allocate a new array that's one element bigger than the old one
  // This should load the %size
  %old_size <- load %old_stack_ptr
  %new_size <- %old_size + 1
  %encoded_new_size <- call @encode(%new_size)
  %default <- 0
  %encoded_default <- call @encode(%default)
  %new_stack_ptr <- call allocate(%encoded_new_size, %encoded_default)

  // Next, write the new value into the first idx of the new array
  %encoded_val <- call @encode(%val)
  %curr_idx <- %new_stack_ptr + 8
  store %curr_idx <- %encoded_val

  // Finally, call a recursive helper function to fill the rest of the array
  %final_stack_ptr <- call @refill_stack(%new_stack_ptr, %old_stack_ptr, %old_size)
  return %final_stack_ptr
}

// Args:    %new_stack_ptr   : ptr
//          %old_stack_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_stack_ptr   : ptr
define @refill_stack(%new_stack_ptr, %old_stack_ptr, %num_remaining) {

  // First, check if we're done
  %more_to_write <- %num_remaining > 0
  br %more_to_write :write_another
  br :base_case

// Base case: stack has been fully reconstructed. Return the pointer
:base_case
  return %new_stack_ptr

// Other case: we need to write more into the stack
:write_another
  // First, get the element that we need to write
  %old_size <- load %old_stack_ptr
  // First element in the array
  %old_stack_loc <- %old_stack_ptr + 8
  %num_elem_offset <- %old_size - %num_remaining
  %offset <- %num_elem_offset * 8
  %old_stack_loc <- %old_stack_loc + %offset
  %val_to_write <- load %old_stack_loc

  // Now, get a pointer to the second element in the new stack
  // Because we've already pushed the first element
  %new_stack_loc <- %new_stack_ptr + 16

  // Then, go to the index that we need to write to
  %new_stack_loc <- %new_stack_loc + %offset
  store %new_stack_loc <- %val_to_write

  // And finally, recur
  %new_num_remaining <- %num_remaining - 1
  %new_stack_ptr <- call @refill_stack(%new_stack_ptr, %old_stack_ptr, %new_num_remaining)
  return %new_stack_ptr
}


// Args:    %new_queue_ptr   : ptr
//          %old_queue_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_queue_ptr   : ptr
define @refill_queue(%new_queue_ptr, %old_queue_ptr, %num_remaining) {

  // First, check if we're done
  %more_to_write <- %num_remaining > 0
  br %more_to_write :write_another
  br :base_case

// Base case: queue has been fully reconstructed. Return the pointer
:base_case
  return %new_queue_ptr

// Other case: we need to write more into the queue
:write_another
  // First, get the element that we need to write
  %old_size <- load %old_queue_ptr
  // First element in the array
  %old_queue_loc <- %old_queue_ptr + 8
  %num_elem_offset <- %old_size - %num_remaining
  %offset <- %num_elem_offset * 8
  %old_queue_loc <- %old_queue_loc + %offset
  %val_to_write <- load %old_queue_loc

  // Now, get a pointer to the second element in the new queue
  // Because we've already pushed the first element
  %new_queue_loc <- %new_queue_ptr + 8

  // Then, go to the index that we need to write to
  %new_queue_loc <- %new_queue_loc + %offset
  store %new_queue_loc <- %val_to_write

  // And finally, recur
  %new_num_remaining <- %num_remaining - 1
  %new_queue_ptr <- call @refill_queue(%new_queue_ptr, %old_queue_ptr, %new_num_remaining)
  return %new_queue_ptr
}


// Args:    %queue_ptr : ptr
//
// Return:  pop_type_ptr : ptr
//
//          Returns a two element array,
//          Where the first is the element we've popped
//          And the second is a pointer to the array without the element that has been popped
define @dequeue(%queue_ptr) {
  // In C, this would be (assert queue.%size > 0
  // But this is the best we can do in this language
  // This should load the %size
  %old_size <- load %queue_ptr
  %not_empty <- %old_size > 0
  br %not_empty :do_dequeue
  br :abort

:abort
  call tensor-error(1)
  return %queue_ptr

:do_dequeue
  // Read out the first value from the input queue
  %dequeue_loc <- %queue_ptr + 8
  %val <- load %dequeue_loc

  // Write the rest of the values into a new array
  %queue_ptr <- call @shrink_queue(%queue_ptr)

  // Package the return array
  // First, allocate a 2 element array where both elements are the popped value
  %ret_size <- 2
  %encoded_ret_size <- call @encode(%ret_size)
  %ret <- call allocate(%encoded_ret_size, %val)

  // Finally, overwrite the second element to be a pointer to the new array, and return
  %ret_back <- %ret + 16
  store %ret_back <- %queue_ptr
  return %ret
}

// Args:    %old_queue_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_queue_ptr   : ptr
define @shrink_queue(%old_queue_ptr) {
  // Allocate a new array that's one element smaller than the old one
  // This should load the %size
  %old_size <- load %old_queue_ptr
  %new_size <- %old_size - 1
  %encoded_new_size <- call @encode(%new_size)
  %default <- 0
  %encoded_default <- call @encode(%default)
  %new_queue_ptr <- call allocate(%encoded_new_size, %encoded_default)

  // Then, populate it
  %new_queue_ptr <- call @do_shrink_queue(%new_queue_ptr, %old_queue_ptr, %new_size)
  return %new_queue_ptr
}

// Args:    %old_queue_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_queue_ptr   : ptr
define @do_shrink_queue(%new_queue_ptr, %old_queue_ptr, %num_remaining) {

  // First, check if we're done
  %more_to_write <- %num_remaining > 0
  br %more_to_write :write_another
  br :base_case

// Base case: queue has been fully reconstructed. Return the pointer
:base_case
  return %new_queue_ptr

// Other case: we need to write more into the queue
:write_another
  %old_size <- load %new_queue_ptr
  %num_elem_offset <- %old_size - %num_remaining
  %offset <- %num_elem_offset * 8
  %old_queue_loc <- %old_queue_ptr + 16
  %old_queue_loc <- %old_queue_loc + %offset
  %val_to_write <- load %old_queue_loc

  %new_queue_loc <- %new_queue_ptr + 8
  %new_queue_loc <- %new_queue_loc + %offset
  store %new_queue_loc <- %val_to_write

  // And finally, recur
  %new_num_remaining <- %num_remaining - 1
  %new_queue_ptr <- call @do_shrink_queue(%new_queue_ptr, %old_queue_ptr, %new_num_remaining)
  return %new_queue_ptr
}


// Args:    %stack_ptr : ptr
//
// Return:  pop_type_ptr : ptr
//
//          Returns a two element array,
//          Where the first is the element we've popped
//          And the second is a pointer to the array without the element that has been popped
define @pop(%stack_ptr) {
  // In C, this would be (assert stack.%size > 0
  // But this is the best we can do in this language
  // This should load the %size
  %old_size <- load %stack_ptr
  %not_empty <- %old_size > 0
  br %not_empty :do_pop
  br :abort

:abort
  call tensor-error(1)
  return %stack_ptr

:do_pop
  // Read out the first value from the input stack
  %pop_loc <- %stack_ptr + 8
  %val <- load %pop_loc

  // Write the rest of the values into a new array
  %stack_ptr <- call @shrink_stack(%stack_ptr)

  // Package the return array
  // First, allocate a 2 element array where both elements are the popped value
  %ret_size <- 2
  %encoded_ret_size <- call @encode(%ret_size)
  %ret <- call allocate(%encoded_ret_size, %val)

  // Finally, overwrite the second element to be a pointer to the new array, and return
  %ret_back <- %ret + 16
  store %ret_back <- %stack_ptr
  return %ret
}

// Args:    %old_stack_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_stack_ptr   : ptr
define @shrink_stack(%old_stack_ptr) {
  // Allocate a new array that's one element smaller than the old one
  // This should load the %size
  %old_size <- load %old_stack_ptr
  %new_size <- %old_size - 1
  %encoded_new_size <- call @encode(%new_size)
  %default <- 0
  %encoded_default <- call @encode(%default)
  %new_stack_ptr <- call allocate(%encoded_new_size, %encoded_default)

  // Then, populate it
  %new_stack_ptr <- call @do_shrink_stack(%new_stack_ptr, %old_stack_ptr, %new_size)
  return %new_stack_ptr
}

// Args:    %old_stack_ptr   : ptr
//          %num_remaining   : int
//
// Return:  %new_stack_ptr   : ptr
define @do_shrink_stack(%new_stack_ptr, %old_stack_ptr, %num_remaining) {

  // First, check if we're done
  %more_to_write <- %num_remaining > 0
  br %more_to_write :write_another
  br :base_case

// Base case: stack has been fully reconstructed. Return the pointer
:base_case
  return %new_stack_ptr

// Other case: we need to write more into the stack
:write_another
  %old_size <- load %new_stack_ptr
  %num_elem_offset <- %old_size - %num_remaining
  %offset <- %num_elem_offset * 8
  // Skip the first element in the old array
  %old_stack_loc <- %old_stack_ptr + 16
  %old_stack_loc <- %old_stack_loc + %offset
  %val_to_write <- load %old_stack_loc

  %new_stack_loc <- %new_stack_ptr + 8
  %new_stack_loc <- %new_stack_loc + %offset
  store %new_stack_loc <- %val_to_write

  // And finally, recur
  %new_num_remaining <- %num_remaining - 1
  %new_stack_ptr <- call @do_shrink_stack(%new_stack_ptr, %old_stack_ptr, %new_num_remaining)
  return %new_stack_ptr
}


define @squares(%n) {
  %ret <- %n * %n
  return %ret
}

define @main() {
  %queue_ptr <- call @new_stack_or_queue()
  %stack_ptr <- call @new_stack_or_queue()

  %count <- 20

:keep_fibbing
  %next_sum <- call @squares(%count)
  %queue_ptr <- call @enqueue(%queue_ptr, %next_sum)
  %stack_ptr <- call @push(%stack_ptr, %next_sum)
  %count <- %count - 1
  %more_to_fib <- %count > 0
  br %more_to_fib :keep_fibbing
  br :print_arrays

:print_arrays
  call print(%queue_ptr)
  call print(%stack_ptr)


:do_printing
  %dequeue_package <- call @dequeue(%queue_ptr)
  %stack_pop_package <- call @pop(%stack_ptr)

  // Print the dequeueped value and update the queue ptr
  %dequeueped_loc <- %dequeue_package + 8
  %dequeueped_val <- load %dequeueped_loc
  %dequeueped_queue_ptr_ptr <- %dequeue_package + 16
  %queue_ptr <- load %dequeueped_queue_ptr_ptr
  call print(%dequeueped_val)

  // Print the stack-popped value and update the stack ptr
  %popped_loc <- %stack_pop_package + 8
  %popped_val <- load %popped_loc
  call print(%popped_val)
  %popped_stack_ptr_ptr <- %stack_pop_package + 16
  %stack_ptr <- load %popped_stack_ptr_ptr

  // Print a bunch of 7's to indicate the end of a round
  %sevens <- 777777777
  %encoded_sevens <- call @encode(%sevens)
  call print(%encoded_sevens)

  // Loop until the queue is empty
  %queue_size <- load %queue_ptr
  %more_to_dequeue <- %queue_size > 0
  br %more_to_dequeue :do_printing
  br :out

:out
  return
}
